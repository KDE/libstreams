# Generated by CMake, don't edit, changes will be lost.

@PACKAGE_INIT@

# Set the Strigi version
set(STRIGI_VERSION_MAJOR @STRIGI_VERSION_MAJOR@)
set(STRIGI_VERSION_MINOR @STRIGI_VERSION_MINOR@)
set(STRIGI_VERSION_PATCH @STRIGI_VERSION_PATCH@)
set(STRIGI_VERSION @STRIGI_VERSION_STRING@)


# all the following settings have been added November 16th, 2008
# before that this file was also installed but contained only the
# version information.
# Alex

# this one is used in FindStrigi.cmake to test whether a recent enough
# version of StrigiConfig.cmake has been found
set(STRIGI_CONFIG_FOUND_AND_HAS_COMPLETE_INFORMATION TRUE)

# information for the source incompat. change from 0.5.9 to 0.6.0
# No need to test for it, since here we know exactly what we have.
set(STRIGI_NEEDS_SIGNED_CHAR TRUE)
set(STRIGI_NEEDS_CHAR FALSE)

# install locations
set_and_check(STRIGI_INCLUDE_DIR "@PACKAGE_INCLUDE_DESTINATION@")
set_and_check(STRIGI_LIBRARY_DIR "@PACKAGE_LIB_DESTINATION@")

# find the full path to the libraries and headers
# streamanalyzer
find_library(STRIGI_STREAMANALYZER_LIBRARY_RELEASE
  NAMES streamanalyzer
  HINTS ${STRIGI_LIBRARY_DIR}
)

find_library(STRIGI_STREAMANALYZER_LIBRARY_DEBUG
  NAMES streamanalyzerd
  HINTS ${STRIGI_LIBRARY_DIR}
)

find_path(STRIGI_STREAMANALYZER_INCLUDES
  NAMES strigi/streamanalyzer.h
  HINTS ${STRIGI_INCLUDE_DIR}
)
mark_as_advanced(STRIGI_STREAMANALYZER_INCLUDES)

# streams
find_library(STRIGI_STREAMS_LIBRARY_RELEASE
  NAMES streams
  HINTS ${STRIGI_LIBRARY_DIR}
)
find_library(STRIGI_STREAMS_LIBRARY_DEBUG
  NAMES streamsd
  HINTS ${STRIGI_LIBRARY_DIR}
)

find_path(STRIGI_STREAMS_INCLUDES
  NAMES strigi/streambase.h
  HINTS ${STRIGI_INCLUDE_DIR}
)
mark_as_advanced(STRIGI_STREAMS_INCLUDES)

# strigiqtdbusclient
find_library(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY_RELEASE
  NAMES strigiqtdbusclient
  HINTS ${STRIGI_LIBRARY_DIR}
)
find_library(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY_DEBUG
  NAMES strigiqtdbusclientd
  HINTS ${STRIGI_LIBRARY_DIR}
)

find_path(STRIGI_STRIGIQTDBUSCLIENT_INCLUDES
  NAMES strigi/qtdbus/strigiclient.h
  HINTS ${STRIGI_INCLUDE_DIR}
)
mark_as_advanced(STRIGI_STRIGIQTDBUSCLIENT_INCLUDES)

# Allow the Strigi submodules to be installed in different prefixes
if(STRIGI_STREAMS_INCLUDES)
  list(APPEND STRIGI_INCLUDE_DIRS ${STRIGI_STREAMS_INCLUDES})
endif()
if(STRIGI_STREAMANALYZER_INCLUDES)
  list(APPEND STRIGI_INCLUDE_DIRS ${STRIGI_STREAMANALYZER_INCLUDES})
endif()
if(STRIGI_STRIGIQTDBUSCLIENT_INCLUDES)
  list(APPEND STRIGI_INCLUDE_DIRS ${STRIGI_STRIGIQTDBUSCLIENT_INCLUDES})
endif()
list(REMOVE_DUPLICATES STRIGI_INCLUDE_DIRS)

macro(_STRIGI_SET_LIBRARY_VAR _name)
   if(${_name}_RELEASE  AND  ${_name}_DEBUG)
      set(${_name} optimized "${${_name}_RELEASE}" debug "${${_name}_DEBUG}" )
   else(${_name}_RELEASE  AND  ${_name}_DEBUG)
      if(${_name}_RELEASE)
         set(${_name} "${${_name}_RELEASE}")
      else(${_name}_RELEASE)
         set(${_name} "${${_name}_DEBUG}" )
      endif(${_name}_RELEASE)
   endif(${_name}_RELEASE  AND  ${_name}_DEBUG)
endmacro(_STRIGI_SET_LIBRARY_VAR _name)

_strigi_set_library_var(STRIGI_STREAMANALYZER_LIBRARY)
_strigi_set_library_var(STRIGI_STREAMS_LIBRARY)
_strigi_set_library_var(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY)

